// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.4.10"
    ext.retrofit_version = '2.9.0'

    ext {
        compile_sdk_version = 30
        build_tools_version = "30.0.2"
        version_name = "1.0"
        version_code = 1
        nav_version = "2.3.1"
    }

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.1.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"
    }
}

allprojects {
    apply from: "$rootDir/ktlint.gradle"

    repositories {
        google()
        jcenter()
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                compileSdkVersion compile_sdk_version
                buildToolsVersion build_tools_version

                defaultConfig {
                    minSdkVersion 21
                    versionName version_name
                    versionCode version_code

                    vectorDrawables.useSupportLibrary = true
                }

                dataBinding {
                    enabled = true
                }

                buildTypes {
                    release {
                        minifyEnabled false
                        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                    }
                }

                flavorDimensions "default"
                productFlavors {
                    defaultGist {
                    }

                    gitHubGist {
                    }
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }

                kotlinOptions {
                    jvmTarget = JavaVersion.VERSION_1_8
                }

                dependencies {
                    def androidDependencies = rootProject.ext.androidDependencies
                    androidDependencies.each { implementation "${it.value}" }

                    def androidAnnotationProcessor = rootProject.ext.androidAnnotationProcessor
                    androidAnnotationProcessor.each { kapt "${it.value}" }

                    def commonDependencies = rootProject.ext.commonDependencies
                    commonDependencies.each { implementation "${it.value}" }

                    def commonAnnotationProcessor = rootProject.ext.commonAnnotationProcessor
                    commonAnnotationProcessor.each { kapt "${it.value}" }
                }
            }
        }

        if (project.hasProperty('kotlin')) {

            dependencies {
                def testDependencies = rootProject.ext.testDependencies
                testDependencies.each { testImplementation "${it.value}" }

                def commonDependencies = rootProject.ext.commonDependencies
                commonDependencies.each { implementation "${it.value}" }

                def commonAnnotationProcessor = rootProject.ext.commonAnnotationProcessor
                commonAnnotationProcessor.each { kapt "${it.value}" }

            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply from: 'dependencies.gradle'